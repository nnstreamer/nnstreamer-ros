CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

PROJECT(nnstreamer-ros C CXX)

OPTION(TIZEN "Enable Tizen build mode" OFF)
OPTION(ENABLE_TEST "Enable tests" OFF)

SET(VERSION "0.0.3")
ADD_DEFINITIONS(-DVERSION="${VERSION}")

IF (NOT ROS_VERSION)
  SET(ROS_VERSION "kinetic")
ENDIF (NOT ROS_VERSION)

IF(ROS_BASE_PREFIX)
  SET(CMAKE_PREFIX_PATH "${ROS_BASE_PREFIX}/${ROS_VERSION}")
ELSEIF (NOT CMAKE_PREFIX_PATH)
  ## Set CMAKE_PREFIX_PATH to ROS default path
  SET(CMAKE_PREFIX_PATH "/opt/ros/${ROS_VERSION}")
ENDIF(ROS_BASE_PREFIX)
SET(ENV{LD_LIBRARY_PATH} "${CMAKE_PREFIX_PATH}/lib:$ENV{LD_LIBRARY_PATH}")

IF(ROS_INSTALL_PREFIX)
  ## We need to override CMAKE_INSTALL_PREFIX here
  SET(CMAKE_INSTALL_PREFIX "${ROS_INSTALL_PREFIX}/${ROS_VERSION}")
ELSEIF (NOT CMAKE_INSTALL_PREFIX)
  ## Set CMAKE_INSTALL_PREFIX to ROS default path
  SET(CMAKE_INSTALL_PREFIX "/opt/ros/${ROS_VERSION}")
ENDIF(ROS_INSTALL_PREFIX)

## Configure ROS-related path
SET(ROS_ROOT "${CMAKE_PREFIX_PATH}/ros")
SET(ROS_PACKAGE_PATH "${CMAKE_PREFIX_PATH}/share")
SET(ROS_ETC_DIR "${CMAKE_PREFIX_PATH}/etc/ros")
SET(ROS_INSTALL_LIB_DIR "${CMAKE_INSTALL_PREFIX}/lib")
SET(ROS_INSTALL_BIN_DIR "${CMAKE_INSTALL_PREFIX}/bin")

IF(NOT NNS_INSTALL_PREFIX)
  SET(NNS_INSTALL_PREFIX "/usr")
ENDIF(NOT NNS_INSTALL_PREFIX)

IF(NOT CMAKE_INSTALL_LIBDIR)
  SET(CMAKE_INSTALL_LIBDIR "lib")
ENDIF(NOT CMAKE_INSTALL_LIBDIR)
IF(NOT NNS_INSTALL_LIBDIR)
  SET(NNS_INSTALL_LIBDIR "${NNS_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}")
ENDIF(NOT NNS_INSTALL_LIBDIR)

IF(NOT CMAKE_INSTALL_BINDIR)
  SET(CMAKE_INSTALL_BINDIR "bin")
ENDIF(NOT CMAKE_INSTALL_BINDIR)
IF(NOT NNS_BIN_INSTALL_BINDIR)
  SET(NNS_BIN_INSTALL_BINDIR "${NNS_INSTALL_PREFIX}/${CMAKE_INSTALL_BINDIR}")
ENDIF(NOT NNS_BIN_INSTALL_BINDIR)

IF(NOT CMAKE_INSTALL_INCLUDEDIR)
  SET(CMAKE_INSTALL_INCLUDEDIR "include")
ENDIF(NOT CMAKE_INSTALL_INCLUDEDIR)
IF(NOT NNS_BIN_INSTALL_INCLUDEDIR)
  SET(NNS_BIN_INSTALL_INCLUDEDIR "${NNS_INSTALL_PREFIX}/${CMAKE_INSTALL_INCLUDEDIR}")
ENDIF(NOT NNS_BIN_INSTALL_INCLUDEDIR)

IF(NOT NNS_INSTALL_GSTDIR)
  SET(NNS_INSTALL_GSTDIR "${NNS_INSTALL_LIBDIR}/gstreamer-1.0")
ENDIF(NOT NNS_INSTALL_GSTDIR)

FIND_PACKAGE(PkgConfig REQUIRED)
SET(PKG_COMMON_MODULES
  glib-2.0
  gstreamer-1.0
  nnstreamer
)

IF(TIZEN)
  # Enable dlog of Tizen.
  SET(PKG_COMMON_MODULES ${PKG_COMMON_MODULES} dlog)
ENDIF(TIZEN)

PKG_CHECK_MODULES(PKGS_COMMON REQUIRED ${PKG_COMMON_MODULES})

INCLUDE_DIRECTORIES(
  ./common/include
  ${PKGS_COMMON_INCLUDE_DIRS}
  )

SET(c_flags -Wall -Werror -fPIC -g -std=c89)
SET(cxx_flags -Wall -Werror -fPIC -g -std=c++11)
ADD_COMPILE_OPTIONS(
  "$<$<COMPILE_LANGUAGE:C>:${c_flags}>"
  "$<$<COMPILE_LANGUAGE:CXX>:${cxx_flags}>"
)

ADD_SUBDIRECTORY(common)
ADD_SUBDIRECTORY(gst)

IF(ENABLE_TEST)
  ENABLE_TESTING()
ENDIF(ENABLE_TEST)
